%class Recognizer

%package recognizer

%start MainMap::class_kw

%map MainMap

%%

Default {
  next no_match { finish(); }
}

class_kw {
  next [ ctxt.match_and_consume("class") and ctxt.peek().isspace() ] post_class { consume(); }
}

post_class {
  next [ ctxt.peek().isspace() ]      post_class { consume(); }
  next [ ctxt.peek().isidentifier() ] class_name { save_position(); }
}

class_name {
  next [ ctxt.peek().isdigit() or ctxt.peek().isidentifier() ] class_name             { consume(); }
  next [ ctxt.peek().isspace() ]                               post_class_name_spaces { save_position(); consume(); }
  next [ ctxt.peek() == ":" ]                                  post_colon_space       { save_position(); consume(); }
}

post_class_name_spaces {
  next [ ctxt.peek().isspace() ] post_class_name_spaces { consume(); }
  next [ ctxt.peek() == ":" ]    post_colon_space       { consume(); }
}

post_colon_space {
  next [ ctxt.peek().isspace() ] post_colon { consume(); }
}

post_colon {
  next [ ctxt.peek().isspace() ]      post_colon  { consume(); }
  next [ ctxt.peek().isidentifier() ] parent_pair {}
}

parent_pair {
  next [ ctxt.match("private") and ctxt.peek(7).isspace() and ctxt.peek(8).isidentifier() ]   parent_name { consume(8); save_position(); }
  next [ ctxt.match("protected") and ctxt.peek(9).isspace() and ctxt.peek(10).isidentifier() ] parent_name { consume(10); save_position(); }
  next [ ctxt.match("public") and ctxt.peek(6).isspace() and ctxt.peek(7).isidentifier() ]    parent_name { consume(7); save_position(); }
  next [ ctxt.peek().isidentifier() ]                                                                    parent_name { save_position(); }
}

parent_name {
  next [ ctxt.peek().isdigit() or ctxt.peek().isidentifier() ] parent_name             { consume(); }
  next [ ctxt.peek().isspace() ]                               post_parent_name_spaces { save_position(); consume(); }
  next [ ctxt.peek() == "," ]                                  post_comma              { save_position(); consume(); }
}

post_parent_name_spaces {
  next [ ctxt.peek().isspace() ] post_parent_name_spaces { consume(); }
  next [ ctxt.peek() == "," ]    post_comma              { consume(); }
  next [ ctxt.peek() == "{" ]    curlies                 { consume(); }
}

post_comma {
  next [ ctxt.peek().isspace() ]      post_comma  { consume(); }
  next [ ctxt.peek().isidentifier() ] parent_pair {}
}

curlies {
  next [ ctxt.peek() == "}" ] post_curlies { consume(); }
}

post_curlies {
  next [ ctxt.peek() == ";" ] end { consume(); }
}

end {
  next [ ctxt.peek() == "" ] match { finish(); }
}

no_match {}

match {
  next match { finish(); }
}

%%
