%class Recognizer

%package recognizer

%start MainMap::class_kw

%map MainMap

%%

Default {
  next no_match { finish(); }
}

class_kw {
  next [ ctxt.match_and_consume("class") and ctxt.peek().isspace() ] post_class { consume(); }
}

post_class {
  next [ ctxt.peek().isspace() ]      post_class { consume(); }
  next [ ctxt.peek().isidentifier() ] class_name { push_position(); }
}

class_name {
  next [ ctxt.peek().isdigit() or ctxt.peek().isidentifier() ] class_name             { consume(); }
  next [ ctxt.peek().isspace() ]                               post_class_name_spaces { push_position(); consume(); }
  next [ ctxt.peek() == ":" ]                                  post_colon_space       { push_position(); consume(); }
}

post_class_name_spaces {
  next [ ctxt.peek().isspace() ] post_class_name_spaces { consume(); }
  next [ ctxt.peek() == ":" ]    post_colon_space       { consume(); }
}

post_colon_space {
  next [ ctxt.peek().isspace() ] post_colon { consume(); }
}

post_colon {
  next [ ctxt.peek().isspace() ]      post_colon  { consume(); }
  next [ ctxt.peek().isidentifier() ] parent_pair {}
}

parent_pair {
  next [ ctxt.match("private") and ctxt.peek(7).isspace() ]   parent_pair_middle { push_position(); consume(7); push_position(); }
  next [ ctxt.match("protected") and ctxt.peek(9).isspace() ] parent_pair_middle { push_position(); consume(9); push_position(); }
  next [ ctxt.match("public") and ctxt.peek(6).isspace() ]    parent_pair_middle { push_position(); consume(6); push_position(); }
  next [ ctxt.peek().isidentifier() ]                         parent_name        { push_position(); }
}

parent_pair_middle {
  next [ ctxt.peek().isspace() ]      parent_pair_middle { consume(); }
  next [ ctxt.peek().isidentifier() ] parent_name        { pop_position(); pop_position(); push_position(); }
  next [ ctxt.peek() == "," ]         post_comma         { consume(); }
}

parent_name {
  next [ ctxt.peek().isdigit() or ctxt.peek().isidentifier() ] parent_name             { consume(); }
  next [ ctxt.peek().isspace() ]                               post_parent_name_spaces { push_position(); consume(); }
  next [ ctxt.peek() == "," ]                                  post_comma              { push_position(); consume(); }
}

post_parent_name_spaces {
  next [ ctxt.peek().isspace() ] post_parent_name_spaces { consume(); }
  next [ ctxt.peek() == "," ]    post_comma              { consume(); }
  next [ ctxt.peek() == "{" ]    curlies                 { consume(); }
}

post_comma {
  next [ ctxt.peek().isspace() ]      post_comma  { consume(); }
  next [ ctxt.peek().isidentifier() ] parent_pair {}
}

curlies {
  next [ ctxt.peek() == "}" ] post_curlies { consume(); }
}

post_curlies {
  next [ ctxt.peek() == ";" ] end { consume(); }
}

end {
  next [ ctxt.peek() == "" ] match { finish(); }
}

no_match {}

match {
  next match { finish(); }
}

%%
